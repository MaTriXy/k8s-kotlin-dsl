buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.fkorotkov:gradle-libraries-plugin:0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'com.fkorotkov.libraries'
apply plugin: "com.jfrog.bintray" // workaround for https://stackoverflow.com/a/48712362

updateLibraries.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['dev', 'eap', 'M1'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('dev version')
            }
        }
    }
}

subprojects {
    ext {
        current_k8s_dsl_version = System.getenv().getOrDefault("CIRRUS_TAG", "2.0.0-SNAPSHOT")
    }

    buildscript {
        repositories {
            maven { setUrl("https://repo.gradle.org/gradle/repo") }
        }

        dependencies {
            classpath libraries['org.jetbrains.kotlin:kotlin-gradle-plugin']
        }
    }

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { setUrl("https://repo.gradle.org/gradle/repo") }
        maven { setUrl("https://artifactory.cronapp.io/public-release") }
    }

    apply plugin: 'com.fkorotkov.libraries'
    apply plugin: "java"
    apply plugin: "jacoco"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    jacocoTestReport {
        group = "Reporting"
        reports {
            xml.enabled true
            csv.enabled false
        }
    }
}